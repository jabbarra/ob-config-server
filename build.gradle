plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'pmd'
	id "com.github.spotbugs" version "4.0.5"
	id 'jacoco'
	id "org.sonarqube" version "2.8"
}

group = 'com.obarra'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
	implementation 'org.springframework.cloud:spring-cloud-config-server'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

check.dependsOn jacocoTestReport, jacocoTestCoverageVerification

test {
	useJUnitPlatform()
}

pmd {
	consoleOutput = true
	toolVersion = "6.21.0"
	sourceSets = [sourceSets.main]
	rulePriority = 5
	ruleSetConfig = resources.text.fromFile("config/pmd/custom-pmd-rules.xml")
}

tasks.withType(Checkstyle) {
	exclude '**/*Application*'
}


jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 1.0
			}
			element = 'METHOD'
			excludes = [
					'com.obarra.obconfigserver.ObConfigServerApplication.main(java.lang.String[])'
			]
		}
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
}
